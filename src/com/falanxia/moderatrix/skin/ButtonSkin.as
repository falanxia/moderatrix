/*
 * Falanxia Moderatrix.
 *
 * Copyright (c) 2010 Falanxia (http://falanxia.com)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

package com.falanxia.moderatrix.skin {
	import com.falanxia.moderatrix.enums.*;
	import com.falanxia.moderatrix.interfaces.*;
	import com.falanxia.utilitaris.utils.*;

	import flash.display.*;
	import flash.geom.*;



	/**
	 * Button skin.
	 *
	 * Button skin to be used with the Button widget.
	 *
	 * @author Vaclav Vancura @ Falanxia a.s. <vaclav@falanxia.com>
	 * @author Falanxia (<a href="http://falanxia.com">falanxia.com</a>, <a href="http://twitter.com/falanxia">@falanxia</a>)
	 * @since 1.0
	 */
	public class ButtonSkin extends Skin implements ISkin, IAssetSkin {


		protected var _hoverInDuration:Number;
		protected var _hoverOutDuration:Number;
		protected var _focusInDuration:Number;
		protected var _focusOutDuration:Number;
		protected var _assetSources:Vector.<BitmapData>;

		private var oldHoverInDuration:Number;
		private var oldHoverOutDuration:Number;
		private var oldFocusInDuration:Number;
		private var oldFocusOutDuration:Number;



		/**
		 * Constructor.
		 * ID is autogenerated if it's empty
		 * @param id Skin ID (optional)
		 */
		public function ButtonSkin(id:String = null) {
			super(SkinType.BUTTON, id);

			_hoverInDuration = 0;
			_hoverOutDuration = 0.15;
			_focusInDuration = 0;
			_focusOutDuration = 0.1;

			_assetSources = new Vector.<BitmapData>;

			_assetSources[0] = new BitmapData(1, 1, true, 0x00000000);
			_assetSources[1] = new BitmapData(1, 1, true, 0x00000000);
			_assetSources[2] = new BitmapData(1, 1, true, 0x00000000);
			_assetSources[3] = new BitmapData(1, 1, true, 0x00000000);
		}



		/**
		 * Destroys the ButtonSkin instance and frees it for GC.
		 */
		override public function destroy():void {
			super.destroy();

			_assetSources[0].dispose();
			_assetSources[1].dispose();
			_assetSources[2].dispose();
			_assetSources[3].dispose();

			_assetSources = null;
		}



		/**
		 * Get assets from vector of BitmapData.
		 * @param value Source vector of BitmapData
		 */
		public function getAssetsFromAtlas(value:Vector.<BitmapData>):void {
			var buttonSource:BitmapData = value[0];

			if(buttonSource.width % 4 != 0) throw new Error("Width has to be multiple of 4");

			_assetSize.width = buttonSource.width / 4;
			_assetSize.height = buttonSource.height;

			_assetSources[0] = BitmapUtils.crop(buttonSource, new Rectangle(0, 0, _assetSize.width, _assetSize.height));
			_assetSources[1] = BitmapUtils.crop(buttonSource, new Rectangle(_assetSize.width, 0, _assetSize.width, _assetSize.height));
			_assetSources[2] = BitmapUtils.crop(buttonSource, new Rectangle(_assetSize.width * 2, 0, _assetSize.width, _assetSize.height));
			_assetSources[3] = BitmapUtils.crop(buttonSource, new Rectangle(_assetSize.width * 3, 0, _assetSize.width, _assetSize.height));
		}



		/**
		 * Parse config Object.
		 * @param value Config Object
		 */
		override public function parseConfig(value:Object):void {
			super.parseConfig(value);

			oldHoverInDuration = _hoverInDuration;
			oldHoverOutDuration = _hoverOutDuration;
			oldFocusInDuration = _focusInDuration;
			oldFocusOutDuration = _focusOutDuration;

			if(value.hoverInDuration != undefined) _hoverInDuration = value.hoverInDuration;
			if(value.hoverOutDuration != undefined) _hoverOutDuration = value.hoverOutDuration;
			if(value.focusInDuration != undefined) _focusInDuration = value.focusInDuration;
			if(value.focusOutDuration != undefined) _focusOutDuration = value.focusOutDuration;
		}



		/**
		 * Revert config to the last known state.
		 */
		override public function revertConfig():void {
			super.revertConfig();

			_hoverInDuration = oldHoverInDuration;
			_hoverOutDuration = oldHoverOutDuration;
			_focusInDuration = oldFocusInDuration;
			_focusOutDuration = oldFocusOutDuration;
		}



		/**
		 * Set hover in duration.
		 * @param value Hover in duration
		 */
		public function set hoverInDuration(value:Number):void {
			_hoverInDuration = value;
		}



		/**
		 * Get hover in duration.
		 * @return Hover in duration
		 */
		public function get hoverInDuration():Number {
			return _hoverInDuration;
		}



		/**
		 * Set hover out duration.
		 * @param value Hover out duration
		 */
		public function set hoverOutDuration(value:Number):void {
			_hoverOutDuration = value;
		}



		/**
		 * Get hover out duration.
		 * @return Hover out duration
		 */
		public function get hoverOutDuration():Number {
			return _hoverOutDuration;
		}



		/**
		 * Set focus in duration.
		 * @param value Focus in duration
		 */
		public function set focusInDuration(value:Number):void {
			_focusInDuration = value;
		}



		/**
		 * Get focus in duration.
		 * @return Focus in duration
		 */
		public function get focusInDuration():Number {
			return _focusInDuration;
		}



		/**
		 * Get focus out duration.
		 * @param value Focus out duration
		 */
		public function set focusOutDuration(value:Number):void {
			_focusOutDuration = value;
		}



		/**
		 * Get focus out duration.
		 * @return Focus out duration
		 */
		public function get focusOutDuration():Number {
			return _focusOutDuration;
		}



		/**
		 * Set asset source BitmapData
		 * @param value Vector of asset source BitmapData
		 */
		public function set assetSources(value:Vector.<BitmapData>):void {
			checkSize(value[0]);
			checkSize(value[1]);
			checkSize(value[2]);
			checkSize(value[3]);

			_assetSources = value;
		}



		/**
		 * Get asset source BitmapData
		 * @return Vector of asset source BitmapData
		 */
		public function get assetSources():Vector.<BitmapData> {
			return _assetSources;
		}
	}
}
