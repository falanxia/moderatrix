/*
 * Falanxia Moderatrix.
 *
 * Copyright (c) 2010 Falanxia (http://falanxia.com)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

package com.falanxia.moderatrix.skin {
	import com.falanxia.moderatrix.enums.*;
	import com.falanxia.moderatrix.interfaces.*;
	import com.falanxia.utilitaris.utils.*;

	import flash.display.*;
	import flash.geom.*;
	import flash.utils.*;



	/**
	 * Button skin.
	 *
	 * Button skin to be used with the Button widget.
	 *
	 * @author Vaclav Vancura @ Falanxia a.s. <vaclav@falanxia.com>
	 * @author Falanxia (<a href="http://falanxia.com">falanxia.com</a>, <a href="http://twitter.com/falanxia">@falanxia</a>)
	 * @since 1.0
	 */
	public class ButtonSkin extends Skin implements ISkin, IAssetSkin {


		public static const GUIDE_ASSET:uint = 0;
		public static const OUT_ASSET:uint = 1;
		public static const HOVER_ASSET:uint = 2;
		public static const FOCUS_ASSET:uint = 3;

		protected var _assetSources:Vector.<BitmapData>;



		/**
		 * Constructor.
		 * ID is autogenerated if it's empty
		 * @param id Skin ID (optional)
		 */
		public function ButtonSkin(id:String = null) {
			super(SkinType.BUTTON, id);

			_assetSources = new Vector.<BitmapData>;

			_assetSources[GUIDE_ASSET] = new BitmapData(1, 1, true, 0x00000000);
			_assetSources[OUT_ASSET] = new BitmapData(1, 1, true, 0x00000000);
			_assetSources[HOVER_ASSET] = new BitmapData(1, 1, true, 0x00000000);
			_assetSources[FOCUS_ASSET] = new BitmapData(1, 1, true, 0x00000000);
		}



		/**
		 * Destroys the ButtonSkin instance and frees it for GC.
		 */
		override public function destroy():void {
			super.destroy();

			_assetSources[GUIDE_ASSET].dispose();
			_assetSources[OUT_ASSET].dispose();
			_assetSources[HOVER_ASSET].dispose();
			_assetSources[FOCUS_ASSET].dispose();

			_assetSources = null;
		}



		/**
		 * Get assets from vector of BitmapData.
		 * @param value Source vector of BitmapData
		 */
		public function getAssetsFromAtlas(value:Vector.<BitmapData>):void {
			var buttonSource:BitmapData = value[0];

			if(buttonSource.width % 4 != 0) throw new Error("Width has to be multiple of 4");

			_assetSize.width = buttonSource.width / 4;
			_assetSize.height = buttonSource.height;

			_assetSources[GUIDE_ASSET] = BitmapUtils.crop(buttonSource, new Rectangle(0, 0, _assetSize.width, _assetSize.height));
			_assetSources[OUT_ASSET] = BitmapUtils.crop(buttonSource, new Rectangle(_assetSize.width, 0, _assetSize.width, _assetSize.height));
			_assetSources[HOVER_ASSET] = BitmapUtils.crop(buttonSource, new Rectangle(_assetSize.width * 2, 0, _assetSize.width, _assetSize.height));
			_assetSources[FOCUS_ASSET] = BitmapUtils.crop(buttonSource, new Rectangle(_assetSize.width * 3, 0, _assetSize.width, _assetSize.height));
		}



		/**
		 * Set asset source BitmapData
		 * @param value Vector of asset source BitmapData
		 */
		public function set assetSources(value:Vector.<BitmapData>):void {
			checkSize(value[GUIDE_ASSET]);
			checkSize(value[OUT_ASSET]);
			checkSize(value[HOVER_ASSET]);
			checkSize(value[FOCUS_ASSET]);

			_assetSources = value;
		}



		/**
		 * Get asset source BitmapData
		 * @return Vector of asset source BitmapData
		 */
		public function get assetSources():Vector.<BitmapData> {
			return _assetSources;
		}



		override protected function resetSettings():Dictionary {
			var set:Dictionary = new Dictionary();

			set["hoverInDuration"] = 0;
			set["hoverOutDuration"] = 0.15;
			set["focusInDuration"] = 0;
			set["focusOutDuration"] = 0.1;

			return set;
		}
	}
}
